@ECHO OFF
REM CONFIGURE.BAT -- Configure MPS distribution for building on Windows.
REM Richard Brooksby, Ravenbrook Limited, 2006-05-24
REM $Id$

IF "%MSVCDir%" == "" GOTO NOVARS

ECHO Checking for Jam/MR...
IF EXIST bin\jam.exe GOTO GOTJAM
CD tool\jam
NMAKE /nologo CC=cl CFLAGS="/nologo /DNT" EXENAME=".\jam0.exe" .\jam0.exe
CD ..\..
IF NOT EXIST bin MKDIR bin
COPY tool\jam\jam0.exe bin\jam.exe

:GOTJAM
ECHO Generating Makefile...
ECHO # Makefile -- Stub Makefile which invokes Jam/MR to do the actual work.>Makefile
ECHO # Generated by $Id$>>Makefile
ECHO. >>Makefile
ECHO all: .phony>>Makefile
ECHO 	@bin\jam>>Makefile
ECHO. >>Makefile
ECHO clean: .phony>>Makefile
ECHO 	@bin\jam clean>>Makefile
ECHO. >>Makefile
ECHO install: .phony>>Makefile
ECHO 	@bin\jam install>>Makefile
ECHO. >>Makefile
ECHO uninstall: .phony>>Makefile
ECHO 	@bin\jam uninstall>>Makefile
ECHO. >>Makefile
ECHO .phony:>>Makefile
GOTO DONE

:NOVARS
ECHO You must run VCVARS32 or equivalent to set up the environment
ECHO before running CONFIGURE.BAT.
GOTO DONE

:DONE
